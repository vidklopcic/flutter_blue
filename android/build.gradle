group 'com.pauldemarco.flutter_blue'
version '1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.0'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.9.4'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'

android {
    if (project.android.hasProperty("namespace")) {
        namespace 'com.pauldemarco.flutter_blue'
    }

    compileSdkVersion 33

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 19
    }

    sourceSets {
        main {
            proto {
                srcDir '../protos'
            }
        }
    }
}

protobuf {
    // Configure the protoc executable
    protoc {
        if (osdetector.os == "osx") {
            artifact = 'com.google.protobuf:protoc:3.9.1:osx-x86_64'
        } else {
            artifact = 'com.google.protobuf:protoc:3.9.1'
        }
    }
    plugins {
        javalite {
            if (osdetector.os == "osx") {
                artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0:osx-x86_64'
            } else {
                artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
            }
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite { }
            }
        }
    }
}

dependencies {
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'
}
